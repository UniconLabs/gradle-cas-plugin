plugins {
    id "groovy"
    id "com.jfrog.bintray" version "1.7"
    id "signing"
    id "maven"
}

repositories {
    jcenter()
    maven { url 'http://repo.spring.io/release'}
}
dependencies {
    compile localGroovy()
    compile gradleApi()
    compile "org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE"
    compile 'org.bitbucket.b_c:jose4j:0.4.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = 3.1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

bintray {
    user = bintrayUsername
    key = bintrayAPIKey

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'uniconiam'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/UniconLabs/gradle-cas-plugin'

        version {
            name = project.version
        }
    }
}